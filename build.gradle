apply plugin: "base"
apply plugin: "maven-publish"

import groovy.json.JsonSlurper

def packageSlurper = new JsonSlurper()
def packageJson = packageSlurper.parse(file('package.json'))
def packageVersion = packageJson.version.toUpperCase()

task assembleArtifact(type: Zip) {
  from 'build/'
  include '**/*'
  exclude '*.zip'
  archiveName "ecos-ui-${packageVersion}.zip"
  destinationDir(file('build'))
}

if (project.hasProperty("mavenUrl")) {

  def artifactId = "ecos-ui"
  if (project.hasProperty("mavenArtifactId")) {
    artifactId = project.getProperty("mavenArtifactId")
  }
  publishing {
    publications {
      uiBuild(MavenPublication) {
        version = packageVersion
        groupId = 'ru.citeck.ecos.ui'
        artifactId = artifactId
        artifact source: assembleArtifact, extension: 'zip'
      }
    }
    repositories {
      maven {
        name 'maven-repo'
        credentials {
          username project.getProperty("mavenUser")
          password project.getProperty("mavenPass")
        }
        url project.getProperty("mavenUrl")
      }
    }
  }

  assemble.dependsOn assembleArtifact
  build.dependsOn assemble

  publish.dependsOn build
}
